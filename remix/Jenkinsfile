def RANCHER_USER = ""
def NODE_ENV = ""
def APP_ENV = ""

pipeline {
    agent {
        label "docker"
    }
    tools {
        nodejs "NodeJS 20.10.0"
    }
    options {
        timeout(time: 20, unit: "MINUTES")
    }

    environment {
        DOCKER_HOST = "tcp://127.0.0.1:2375"
        DOCKER_REGISTRY = "oprdocker.eficode.com"
        RANCHER_URL = "https://oprrancher.eficode.com"
        SONAR_PROPERTIES_EXISTS = fileExists "sonar-project.properties"
        UI_PORT = 4874
        UI_NETWORK = "public"
        UI_NETWORK_GROUP = "host1"
        APP = "kyc"
        APP_PATH = "apps/${APP}"
        APP_PORT = 5001
        PROJECT_NAME = "frontend"
        SERVICE_NAME = "${PROJECT_NAME}-${APP}"
        IMAGE_PATH = "${DOCKER_REGISTRY}/${PROJECT_NAME}/${SERVICE_NAME}/${BRANCH_NAME}"
        // Following ones are set later in code
        // APP_VERSION = ""
        // GIT_REPO_URL = ""
        // NODE_ENV = ""
    }

    stages {
        stage("Checkout") {
            steps {
                checkout scm
            }
        }

        stage("SonarQube Analysis") {
            when {
                expression { SONAR_PROPERTIES_EXISTS == "true" }
            }
            steps {
                script {
                    scannerHome = tool "Sonarqube latest"
                }
                withSonarQubeEnv("SonarQube 5.6.6 (oprsonar)") {
                    sh "${scannerHome}/bin/sonar-scanner"
                }
            }
        }

        stage("Read versions and set variables") {
            steps {
                script {
                    def props = readProperties file:"${APP_PATH}/versions.env"

                    if(env.BRANCH_NAME == 'main' || env.BRANCH_NAME == "master" || env.BRANCH_NAME.startsWith('hotfix/')) {
                        env.APP_VERSION = props['PRODUCTION_VERSION'].replace('"', '')
                        NODE_ENV = "production"
                        APP_ENV = "production"
                        RANCHER_USER = "on-prem-prod"
                    } else {
                        env.APP_VERSION = props['DEVELOPMENT_VERSION'].replace('"', '')
                        NODE_ENV = "development"
                        APP_ENV = "development"
                        RANCHER_USER = "on-prem-dev"
                    }

                    echo "env.APP_VERSION = ${env.APP_VERSION}"
                    def repoUrl = env.GIT_URL
                    repoUrl = repoUrl.replace("https://", "")
                    env.GIT_REPO_URL = repoUrl
                }
            }
        }

        stage("Generate files") {
            when {
                expression { env.BRANCH_NAME ==~ /(?i)(main|master|development|hotfix\/.*)/}
            }
            steps {
                script {
                    def dockerComposeTemplate = readFile("${APP_PATH}/pipelineResources/docker-compose.yml.template")
                    def rancherComposeTemplate = readFile("${APP_PATH}/pipelineResources/rancher-compose.yml.template")
                    
                    def envFileName = "${APP_PATH}/.env." + NODE_ENV
                    def envFileContent = readFile(envFileName)
                    def envEntries = envFileContent.readLines()
                        .findAll { it && !it.startsWith("#") } // skip comments and blanks
                        .collect { "      - ${it.trim()}" }   // indent correctly for YAML
                    // Add node env
                    // envEntries.add("      - NODE_ENV=" + NODE_ENV)

                    def generatedDockerCompose = dockerComposeTemplate
                        .replace('${SERVICE_NAME}', env.SERVICE_NAME)
                        .replace('${IMAGE_PATH}', env.IMAGE_PATH)
                        .replace('${APP_VERSION}', env.APP_VERSION)
                        .replace('${UI_PORT}', env.UI_PORT)
                        .replace('${PORT}', env.APP_PORT)
                        .replace('${NODE_ENV}', NODE_ENV)
                        .replace('${APP_ENV}', APP_ENV)
                        .replace('${UI_NETWORK}', env.UI_NETWORK)
                        .replace('${UI_NETWORK_GROUP}', env.UI_NETWORK_GROUP)
                        .replace("environment:", "environment:\n${envEntries.join('\n')}")

                    writeFile file: 'docker-compose.yml', text: generatedDockerCompose

                    def generatedRancherCompose = rancherComposeTemplate
                        .replace('${SERVICE_NAME}', env.SERVICE_NAME)

                    writeFile file: 'rancher-compose.yml', text: generatedRancherCompose

                    sh'''
                        cp ${APP_PATH}/pipelineResources/Dockerfile Dockerfile

                        echo "docker-compose.yml:"
                        cat docker-compose.yml

                        echo "Dockerfile ui:"
                        cat Dockerfile

                        echo "rancher-compose.yml"
                        cat rancher-compose.yml
                    '''
                }
            }
        }

        stage("Docker registry login") {
            steps {
                withCredentials([string(credentialsId: "artifactory-access", variable: 'SECRET')]) {
                    sh "docker login --username opr-jenkins --password $SECRET ${DOCKER_REGISTRY}"
                }
            }
        }

        stage("Build Docker image") {
            steps {
                sh "docker build --build-arg APP_TO_BUILD=\'${APP}\' --build-arg APP_ENV=\'${APP_ENV}\' --build-arg CURRENT_ENV=\'${NODE_ENV}\' --build-arg PORT=${APP_PORT} -t ${IMAGE_PATH}:${APP_VERSION} ."
            }
        }

        stage("Tag Docker image") {
            steps {
                sh "docker tag ${IMAGE_PATH}:${APP_VERSION} ${IMAGE_PATH}:latest"
            }
        }

        stage("Push image to registry") {
            steps {
                sh "docker push ${IMAGE_PATH}:${APP_VERSION}"
                sh "docker push ${IMAGE_PATH}:latest"
            }
        }

        stage("Tag git") {
            steps {
                withCredentials([usernamePassword(credentialsId: 'jenkins-github-access',
                    usernameVariable: 'GITHUB_APP',
                    passwordVariable: 'GITHUB_ACCESS_TOKEN')]) {
                    sh "git tag GH-$BRANCH_NAME-${APP_VERSION}"
                    sh "git push https://$GITHUB_APP:$GITHUB_ACCESS_TOKEN@${GIT_REPO_URL} --tags"
                }
            }
        }
        stage("Deploy image") {
            steps {
                withCredentials([[$class: 'UsernamePasswordMultiBinding', 
                    credentialsId: "${RANCHER_USER}", 
                    usernameVariable: 'APIKEY', 
                    passwordVariable: 'APISECRET']]) {
                    sh '''
                        export SERVICE=${SERVICE_NAME}
                        export BRANCH_NAME=${BRANCH_NAME}
                        export BUILD_NUMBER=${BUILD_NUMBER} 

                        rancher-compose -p frontend --secret-key $APISECRET --access-key $APIKEY --url ${RANCHER_URL} up -d --force-upgrade --confirm-upgrade --upgrade ${SERVICE_NAME}
                    '''
                }
            }
        }
    }
}